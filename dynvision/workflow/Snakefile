include: "snake_utils.smk"
include: "snake_data.smk"
include: "snake_runtime.smk"
include: "snake_visualizations.smk"

rule all:
    input:
        expand(project_paths.figures / 'plot_experiments_on_models{model_args}.done',
            model_args = replace_param_in_string(args_product(config.model_args)[0], key="rctype", value_type=str, new_value="*"))

rule all_experiments:
    input:
        expand(project_paths.reports / 'experiment_{experiment}.done',
            experiment = config.experiment)

rule experiment:
    input:
        lambda w: expand(project_paths.reports \
            / '{model_name}' \
            / '{model_name}{model_args}_{seed}_{data_name}_{status}_{data_loader}{data_args}_{data_group}_test_outputs.csv',
            model_name = config.model_name,
            seed = config.seed,
            model_args = args_product(config.model_args),
            data_name = config.data_name,
            data_group = config.data_group,
            status = config.experiment_config[w.experiment]["status"],
            data_loader = config.experiment_config[w.experiment]["data_loader"],
            data_args = args_product(config.experiment_config[w.experiment]["data_args"]))
    output:
        temp(project_paths.reports / 'experiment_{experiment}.done')
    shell:
        """
        touch {output:q}
        """

rule test_standard_models:
    input:
        lambda w: expand(project_paths.reports \
        / '{model_name}' \
        / '{model_name}_{seed}_{data_name}_{status}_{data_loader}{data_args}_{data_group}_test_outputs.csv',
        model_name = ["AlexNet", "CorNetRT", "CordsNet", "Resnet18"],
        seed = "0000",
        data_name ="imagenet",
        data_group = "snakes",
        status = "trained",
        data_loader = config.experiment_config[w.experiment]["data_loader"],
        data_args = args_product(config.experiment_config[w.experiment]["data_args"]))
    output:
        temp(project_paths.reports / 'experiment_standard_models_{experiment}.done')
    shell:
        """
        touch {output:q}
        """